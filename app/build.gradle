plugins {
    id 'com.github.triplet.play' version '3.8.1'
}

apply plugin: 'com.android.application'

def isCi = System.getenv("CI") == "true"
android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.gianlu.dnshero"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 40
        versionName "1.6.14"
    }

    if (isCi) {
        signingConfigs {
            release {
                storeFile file("../.github/keystore.jks")
                storePassword System.getenv("KEYSTORE_STORE_PASS")
                keyAlias = System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isCi) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        foss {
            dimension 'main'
        }
        standard {
            dimension 'main'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        warning 'MissingTranslation', 'Instantiatable'
    }

    playConfigs {
        foss {
            commit.set(false)
            enabled.set(false)
        }
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isCi)
    serviceAccountCredentials.set(file("../.github/secrets.json"))
    track.set("production")
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT)
}

dependencies {
    implementation(project(':CommonUtils')) {
        exclude group: 'com.github.faruktoptas', module: 'FancyShowCaseView'
    }

    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.google.android.material:material:1.9.0'

    standardImplementation 'com.google.firebase:firebase-perf:20.4.0'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'