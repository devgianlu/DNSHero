plugins {
    id 'com.github.triplet.play' version '3.0.0'
}

apply plugin: 'com.android.application'

if (getGradle().getStartParameter().getTaskRequests()
        .toString().contains("Standard")) {
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.firebase.firebase-perf'
}

def isCi = System.getenv("CI") == "true"
android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.gianlu.dnshero"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 35
        versionName "1.6.9"
    }

    if (isCi) {
        signingConfigs {
            release {
                storeFile file("../.github/keystore.jks")
                storePassword System.getenv("KEYSTORE_STORE_PASS")
                keyAlias = System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isCi) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        foss {
            dimension 'main'
        }
        standard {
            dimension 'main'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'MissingTranslation'
    }

    playConfigs {
        foss {
            commit.set(false)
            enabled.set(false)
        }
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isCi)
    serviceAccountCredentials.set(file("../.github/secrets.json"))
    track.set("production")
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT)
}

dependencies {
    implementation(project(':CommonUtils')) {
        exclude group: 'me.toptas.fancyshowcase'
    }

    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.google.android.material:material:1.3.0'

    standardImplementation 'com.google.firebase:firebase-perf:20.0.1'
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

apply plugin: 'com.google.gms.google-services'